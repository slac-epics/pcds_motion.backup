' XIP Pulse Picker Program
' VA restrictions:
' 1. A variable cannot be named after A MCode Instruction,
' Variable or Flag or Keyword
' 2. The first character must be alpha, the second character
' may be alpha-numeric.
' 3. A variable is limited to two characters.
' 4. Limited to 192 variables and labels.

'Additions
VA Ve        'Program version
VA SE        'Program select
VA EN        'Current enable
VA Eo        'Last enable
VA TG        'Trigger mode 3
VA Sd        '+/- deadband
VA Ud        'Upper limit violation flag
VA Ld        'Lower limit violation flag
VA N3        'Cw encoder desired position
VA N4        'Ccw encoder desired position
VA AF        'Actual frequency (120Hz)
VA DF        'Desired frequency
VA RF        'Ratio of frequencies
VA R5

VA NS
VA DT        'Waiting time
VA QN        'Ratio to divide
VA N1        'Steps to move Cw
VA N2        'Steps to move ccw
VA N         'Steps to move
VA CT        'Trigger counter
VA HB        'Hearbeat during normal operation
VA PU
VA EC        'Enable error checking
VA N5        'Cw limit + SD
VA N6        'Ccw limt - SD
VA Go        'Single burst loop indicator
VA N7        'Calc Cw swing open, relative
VA N8        'Calc Ccw swing close, relative
VA N9        'Is Closed flag
VA MO        'Move to open value
VA X1        'Middle enc position
VA X2        'Upper enc deadband
VA V1        'Previous VM
VA V2        'Previous VI
VA V3        'Previous A
VA V4        'Previous D

Ht=5         'Reduce Holding time to 5
Fc=9         'filter input 13 for 12.9uS need this?
R2=0         'Clear R2 for counting
Dn="1"
El=4096      'Encoder lines
S3=16,1,1    'Encoder power
S4=16,0,0    'Setup IO 4 for Fan control
S13=60,0,0   'Set inp 13 for Hi speed TC in
Fc=0         'Filter input none
Ms=256       'Microsteps/Fullsteps
Vi=1000      'Initial velocity
Vm=768000    'Maximum velocity
A=1000000    'Acceleration
D=1000000    'Deceleration
Ee=0         'Disable encoder closed-loop
Rc=100       'Max run current
Hc=5         'Min hold current

'Separate variable assignments from declarations
Ve=3         'MCode Version 3
SE=0
EN=0
Eo=0
TG=0
Sd=50
Ud=0
Ld=0
AF=120
DF=30
RF=AF/DF
NS=0
DT=0
QN=1
N1=3200
N2=-3200
N3=1016
N4=0
N5=N3+SD
N6=N4-SD 
N=5
CT=0
HB=0
PU=0
EC=1        'Default error checking ON
N7=N1/2     'Open swing value
N8=N2/2     'Close swing value
N9=0        'Default to zero, sucesseful close=1
MO=650      'Default move to open, encoder cnts
X1=N3/2
X2=X1+Sd
NE=0        'Disable numeric enable

PG 1

  LB SU
    ' 30Hz testing inits
    Te=0       'Disable trigger
    SE=0       'Program select
    'N1=3200    '+22.5 degrees
    'N2=-3200   '-22.5 degrees
    'N3=1016    '+22.5 degress in counts
    'N4=0       'Home position
    N5=N3+SD
    N6=N4-SD
    'Sd=50      '+/- deadband
    'QN=1
    'DT=0
    CT=0
    'N=5
    Ud=0
    Ld=0
    En=0       'Clear position history
    Eo=0       'Clear position history
    R1=0
    R2=0       'Limit check alternator
    'Vm=5000000
    'Vi=100000
    'A=30000000
    'D=30000000
    Go=0
    N9=0       'Default to zero, sucesseful close=1
    H 100

  LB ZA             ' Main loop
    BR Z0, SE = 1   ' Mode 1
    BR Z2, SE = 2   ' Mode 2
    BR Z4, SE = 3   ' Mode 3
    CL Z8, SE = 4   ' Open routine
    CL Z9, SE = 5   ' Close routine
    IC HB
    H 100
    BR ZA

  LB Z0             ' One-shot init
    CL Y3           'Realignment check
    Tc=M1
    R2=0
    Go=0
    'H 100
    BR Z1

  LB Z1             ' One-shot program, loop
    CL ZB, GO=1
    BR Z1

  LB ZB
    Te = 4
    GO = 0
    RT

  LB M1
    CL ZC, EN = 0
    CL ZD, EN = 1
    CL ZE, EO = 0
    CL ZF, EO = 1
    RT

  LB ZC
    MR N1
    H
    EO = 0
    CL Zp,EC=1     ' Check CW + deadband
    'CL Zq,EC=1     ' Check CW - deadband
    RT

  LB ZD
    MR N2
    H
    EO = 1
    'CL Zr,EC=1    ' Check CCW + deadband
    CL Zs,EC=1    ' Check CCW - deadband
    RT

  LB ZE
    EN = 1
    RT

  LB ZF
    EN = 0
    RT

  LB Z2         ' Flip-flop init
    CL Y3         'Realignment check
    Tc = M2
    Te = 4
    'H 100
    BR Z3

  LB Z3         ' Flip-flop program, loop
    'Run Forever
    BR Z3

   LB M2
    'IC R2
    CL ZC, EN = 0
    CL ZD, EN = 1
    CL ZE, EO = 0
    CL ZF, EO = 1
    Te = 4
    RT

  LB Z4             ' Burst init
    CL Y3           'Realignment check
    R1=0
    BR Yy, TG = 0   ' Single burst
    BR Yz, TG = 1   ' Continuous burs
    BR Ys, TG = 2   ' Open/close only
    BR Z4

  LB Yy
    Tc=M5
    CL Yx, Go=1
    BR Yy
   
  LB Yx
    Te=4
    BR Z6 
    
  LB Z6         ' 3.1 Single burst, loop
    BR Yy, Go=0
    BR Z6

  LB Yz
    Tc=M4
    Te=4
    BR Z5

  LB Z5          ' 3.2 Burst program, loop
    'Run Forever
    BR Z5

  LB Ys
    Tc=M6
    Te=4
    BR Z7

  LB Z7          ' 3.3 Burst program, open/close, loop
    'Run Forever
    BR Z7

  LB M4          ' Continous function
    IC R1
    CL Zx, R1=1
    CL ZZ, R1>=N
    Te=4
    RT

  LB M5          ' Single function
    IC R1
    CL Zx, R1=1
    CL ZZ, R1>=N
    Te=4
    CL Ya, Go=0
    RT

  LB Ya
    Te=0
    RT

  LB M6           ' Open/close function
    IC R1
    CL Zx, R1=1   'Open 1st trigger
    CL ZZ, R1>=2  'Close 2nd trigger
    Te=4
    RT

  LB Zx      'Move to open
    MR N7
    H
    CL Zt,EC=1
    RT

  LB ZZ      'Move to close, reset
    R1=0
    Go=0
    MR N8
    H
    CL Zs,EC=1
    RT
  
  LB Z8     'Fast open, encoder
    CL Y3   'Align to safe position
    CL Y4   'Save current speeds
    CL Y7   'Fast open/close speeds
    SE=0    'Return status to standby
    EE=1    'Enable encoder
    MA MO   'Move to open position
    H       'Wait for move to finish
    EE=0    'Disable encoder
    CL Y6   'Restore speeds
    RT

  LB Z9     'Fast close, encoder
    CL Y3   'Align to safe position
    CL Y4   'Save current speeds
    CL Y7   'Fast open/close speeds
    SE=0    'Return status to standby
    EE=1    'Enable encoder
    MA 0    'Move to closed, home position
    H       'Wait till move is done
    EE=0    'Disable encoder
    CL Y6   'Restore speeds
    RT

  'LB M3     'Delay segment
  '  MR N1
  '  H DT
  '  MR N2
  '  H DT
  '  RT

  LB Zj            ' Cw slip check
    R3=N3-Sd
    R4=N3+Sd
    CL Zm, C2<R3   ' Slipped below deadband
    CL Zm, C2>R4   ' Slipped above deadband
  RT

  LB Zk             'Ccw slip check
    R3=N4-Sd
    R4=N4+Sd
    CL Zn, C2<R3   ' Slipped below deadband
    CL Zn, C2>R4   ' Slipped above deadband
  RT

  LB Zp            ' CW + deadband
    'PR "CW+ check"
    'R4=N3+Sd
    'CL Zm, C2>R4   ' Slipped above CW deadband
    CL Zm, C2>N5   ' Slipped above CW deadband
    PR "OPEN=", C2
    'IC R2
    RT

  LB Zt            ' CW + Db to "home position" N1/2
    'R4=N3/2
    'R4=R4+Sd
    CL Zm, C2>X2
    PR "OPEN=", C2
    RT

  LB Zq            ' CW - deadband
    'PR "CW- check"
    R3=N3-Sd
    CL Zm, C2<R3   ' Slipped below CW deadband
    'PR "OPEN=", C2
    'IC R2
    RT

  LB Zr            ' CCW + deadband
    'PR "CCW+ check"
    R4=N4+Sd
    CL Zn, C2>R4   ' Slipped above CCW deadband
    'IC R2
    RT

  LB Zs            ' CCW - deadband
    'PR "CCW- check"
    'R3=N4-Sd
    'CL Zn, C2<R3   ' Slipped below CCW deadband
    CL Zn, C2<N6   ' Slipped below CCW deadband
    PR "CLOSE=", C2
    'R2=0
    RT

  LB Zm
    Ud=1          ' Upper limit violation
    PR "ULV=", C2
    'MA 0       'Move to home position
    'CL Y3         ' Move to close
    SE=0
    E             ' Kill program on violation
    RT

  LB Zn
    Ld=1          ' Lower limit violation
    PR "LLV=", C2
    'MA 0       'Move to home position
    'CL Y3         ' Move to close
    SE=0
    E             ' Kill program on violation
    RT

  LB Y1         'Reset far end, C2>N3-SD
    EE=1        'Enable encoder
    MA 8192     'Move to 180 degrees
    H           'Wait till move is done
    C1=0        'Reset microsteps register
    C2=0        'Reset encoder register
    EE=0        'Disable encoder for trigger mode
    N9=1        'Flag successful close
    H 100
    RT

  LB Y2         'Reset near zero, C2<N4+SD
    EE=1        'Enable encoder
    MA 0        'Move to 0 degrees
    H           'Wait till move is done
    C1=0        'Reset microsteps register
    C2=0        'Reset encoder register
    EE=0        'Disable encoder for trigger mode
    N9=1        'Flag successful close
    H 100
    RT

  LB Y3         'Realignment checks
    CL Y4       'Save current speeds
    CL Y5       'Assign realignment speeds
    R3=N3-50      'Lower upper deadband
    R4=N4+50      'Upper lower deadband
    CL Y2, C2<R4  'If C2 is below this db, move to 0
    CL Y1, C2>R3  'If C2 above the db, move to 180
    CL Y1, N9=0   'If open, just move to 0
    CL Y6       'Restore PP speeds
    PR "REALIGN=1"
    H 100
    RT

  LB Y4         'Save current speeds
   V1=VM
   V2=VI
   V3=A
   V4=D
   RT

  LB Y5         'Assign realignment speeds
   VM=8192
   VI=2000
   A=2000
   D=2000
   RT

  LB Y6         'Restore speeds
   VM=V1
   VI=V2
   A=V3
   D=V4
   RT
  
  LB Y7        'Fast open/close speeds
   VM=32000
   VI=10000
   A=32000
   D=32000
   RT

E
PG
